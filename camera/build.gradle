buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.github.dcendents:android-maven-plugin:1.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

version '0.6.25.9'
group 'com.commonsware.cwac.camera'

def publishArtifactId = 'camera'

repositories {
    jcenter()
    mavenCentral()

    maven {
        url 'http://nexus.scanbot.io/nexus/content/repositories/releases/'
    }
    maven {
        url 'http://nexus.scanbot.io/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/releases/'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.android.support:support-v4:19.+'
}

android {
    compileSdkVersion 21
    buildToolsVersion '24.0.1'

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['assets']
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

// from http://stackoverflow.com/a/19484146/115145
android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
    return; // Skip debug builds.
  }
  def task = project.tasks.create "jar${name.capitalize()}", Jar
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
	task.archiveName = "cwac-${task.archiveName}"
}

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: "http://nexus.scanbot.io/nexus/content/repositories/snapshots/") {
                authentication(userName: project.getProperty('NEXUS_LOGIN'), password: project.getProperty('NEXUS_PASSWORD'))
            }
            repository(url: "http://nexus.scanbot.io/nexus/content/repositories/releases/") {
                authentication(userName: project.getProperty('NEXUS_LOGIN'), password: project.getProperty('NEXUS_PASSWORD'))
            }

            pom.version = project.version
            pom.artifactId = publishArtifactId
            pom.groupId = project.group
        }
    }
}
