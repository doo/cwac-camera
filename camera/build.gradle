
buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.6.0"
        classpath "com.github.dcendents:android-maven-plugin:1.0"
    }
}

apply plugin: "com.android.library"
apply plugin: "maven-publish"

group "com.commonsware.cwac.camera"
def publishArtifactId = "camera"
version "0.6.41"

repositories {
    google()
    jcenter()
    mavenCentral()

    maven { url "http://nexus.scanbot.io/nexus/content/repositories/releases/" }
    maven { url "http://nexus.scanbot.io/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
}

dependencies {
    api fileTree(dir: "libs", include: "*.jar")
    api "androidx.legacy:legacy-support-v4:1.0.0"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
    }
}

afterEvaluate {
    publishing {
        publications {
            ScanbotSDK(MavenPublication) {
                groupId = "$project.group"
                artifactId = publishArtifactId
                version = "$project.version"
                from components.release
            }
        }
        repositories {
            maven {
                name = "ScanbotNexus"

                def snapshotsRepoUrl = "http://nexus.scanbot.io/nexus/content/repositories/snapshots/"
                def releasesRepoUrl = "http://nexus.scanbot.io/nexus/content/repositories/releases/"
                url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = project.getProperty("NEXUS_LOGIN")
                    password = project.getProperty("NEXUS_PASSWORD")
                }
            }
        }
    }
}
